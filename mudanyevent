#!/usr/bin/env perl
# vim: set filetype=perl :
use strict;
use warnings;

use lib './lib';

use AnyEvent;
use AnyEvent::Socket;
use AnyEvent::Handle;

use Av4::Telnet qw/%TELOPTS %TELOPTIONS TELOPT_FIRST
    TELOPT_WILL TELOPT_WONT TELOPT_DO TELOPT_DONT
    TELOPT_IAC TELOPT_SB TELOPT_SE TELOPT_COMPRESS2
    TELOPT_TTYPE TELOPT_NAWS TELOPT_MSP TELOPT_MXP
    _256col/;

my $quit_program = AnyEvent->condvar;

my $nclients = 0;
my %clients;

#my $srv = tcp_server(undef, 8081, \&accept_cb, sub {
tcp_server(undef, 8081, \&accept_cb, sub {
    my ($fh, $thishost, $thisport) = @_;
    warn "Listening on $thishost:$thisport\n";
});

$quit_program->recv;

sub accept_cb
{
    my ($fh, $host, $port) = @_;
    my $curr_client = $nclients;
    warn "Connection from $host:$port ($curr_client)\n";
    my $handle;
    $handle = new AnyEvent::Handle(
        fh => $fh,
        on_error => sub {
            warn "Error $_[2]";
            $_[0]->destroy();
        },
        on_eof => sub {
            warn "Goodbye client $curr_client\n";
            delete $clients{$curr_client};
            $handle->destroy; # destroy handle
        },
    );
    $clients{$curr_client} = $fh;

    $handle->push_write( sprintf( "%c%c%c", TELOPT_IAC, TELOPT_WILL, TELOPT_COMPRESS2 ) );
    $handle->push_write( sprintf( "%c%c%c", TELOPT_IAC, TELOPT_DO,   TELOPT_TTYPE ) );
    $handle->push_write( sprintf( "%c%c%c", TELOPT_IAC, TELOPT_DO,   TELOPT_NAWS ) );
    $handle->push_write( sprintf( "%c%c%c", TELOPT_IAC, TELOPT_WILL, TELOPT_MSP ) );
    $handle->push_write( sprintf( "%c%c%c", TELOPT_IAC, TELOPT_WILL, TELOPT_MXP ) );
    $handle->push_write( '#$#mcp version: 2.1 to: 2.1' . "\r\n" );
    $handle->push_write("Hi, Welcome to the MUD!\r\n\r\n"); # FIXME BANNER

    $handle->on_read( sub {
            print "($curr_client) Got: [", join(' ',map { sprintf("(%c(%d))",$_>=32&&$_<=126?$_:126,$_) } unpack('C*',$_[0]->rbuf)), "]\n";
            $handle->push_write($_[0]->rbuf);
            $_[0]->rbuf = '';
        });
    $nclients++;
    ();
}
